PREFIX owl:    <http://www.w3.org/2002/07/owl#>
PREFIX rdf:    <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh:     <http://www.w3.org/ns/shacl#>
PREFIX dc:     <http://purl.org/dc/terms/>
PREFIX skos:   <http://www.w3.org/2004/02/skos/core#>
PREFIX elm:        <http://data.europa.eu/snb/model/elm/>
SELECT DISTINCT ?class ?property ?range ?className ?propertyName ?rangeName ?minCount ?maxCount
WHERE {
    ?classShape sh:targetClass ?class .

    ### Generate label
    OPTIONAL {
        ?class rdfs:label ?classNameOntology
        FILTER(lang(?classNameOntology)='en')
    }
    OPTIONAL {
        ?classShape dc:title ?classNameShacl
        FILTER(lang(?classNameShacl)='en')
    }
    BIND(COALESCE(str(?classNameShacl),str(?classNameOntology),REPLACE(STR(?class), "^.*?([_\\p{L}][-_\\p{L}\\p{N}]*)$", "$1")) as ?className)

    OPTIONAL {
        ?classShape sh:property ?propertyShape .
        ?propertyShape a sh:PropertyShape ;
        sh:path ?property ;

        ### Generate label
        OPTIONAL {
            # bug in ELM model - multiple en props, e.g. for mailbox
            SELECT ?property (sample(?propertyNameXX) AS ?propertyNameOntology) {
                ?property rdfs:label ?propertyNameXX
                FILTER(lang(?propertyNameXX)='en')
            } GROUP BY ?property
        }
        OPTIONAL {
            ?propertyShape sh:name ?propertyNameShacl
            FILTER(lang(?propertyNameShacl)='en')
        }
        BIND(COALESCE(str(?propertyNameShacl), str(?propertyNameOntology),REPLACE(str(?property),"^.*?([_\\p{L}][-_\\p{L}\\p{N}]*)$","$1")) AS ?propertyName)

        OPTIONAL {
            ?propertyShape sh:minCount ?minCountX .
        }
        BIND( COALESCE(?minCountX,0) AS ?minCount)
        OPTIONAL {
            ?propertyShape sh:maxCount ?maxCountX .
        }
        BIND( COALESCE(?maxCountX,"*") AS ?maxCount)
        OPTIONAL {
            ?propertyShape sh:class ?rangeClass .
            OPTIONAL {
                ?rangeClass rdfs:label ?rangeClassNameX
                FILTER(lang(?rangeClassNameX)='en')
            }
            BIND(COALESCE(str(?rangeClassNameX),REPLACE(str(?rangeClass),"^.*?([_\\p{L}][-_\\p{L}\\p{N}]*)$","$1")) AS ?rangeClassName)
        }
        OPTIONAL {
            ?propertyShape sh:datatype ?rangeDatatypeX .
            BIND(REPLACE(str(?rangeDatatypeX),"^.*?([_\\p{L}][-_\\p{L}\\p{N}]*)$","$1") AS ?rangeDatatype)
        }
        BIND(COALESCE(?rangeClassName,?rangeDatatype,"literal") AS ?rangeName)
        BIND(COALESCE(?rangeClass,?rangeDatatypeX) AS ?range)
    }
}
GROUP BY ?class ?property ?range ?className ?propertyName ?rangeName ?minCount ?maxCount ?classDescription ?propertyDescription
ORDER BY ?className DESC(?propertyName) ?minCount ?maxCount ?rangeName